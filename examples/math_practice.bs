Comment math_practice.bs
Comment This is a math practice program

Include input.bs
Include print.bs

List numbers

Function Setup[op]
Begin
	Clear
	
	Var msg
	Define msg as "How many times do you wish to practice? "
	
	Var no_practice
	Define no_practice as (Call Prompt[msg])
	
	Loop no_practice
		Clear
		Call Practice[op]
	Stop
	
	Call Menu2[op]
End

Function Practice[op]
Begin
	Var no1
	Var no2
	Var answer

	Define no1 as (Random 10)
	Define no2 as (Random 10)
	
	Print "Solve: "
	Print no1
	If op Equals "+"
		Define answer as (Add no1 and no2)
		Print " + "
	Else op Equals "-"
		Define answer as (Sub no1 and no2)
		Print " - "
	Else op Equals "x"
		Define answer as (Mp no1 and no2)
		Print " x "
	Else op Equals "/"
		Define answer as (Div no1 and no2)
		Print " / "
	Stop
	
	Print no2
	Print " = "
	
	Var user
	Input user
	
	If user Equals answer
		Color green
		PrintLine "Correct!!"
		Color none
	Else
		Color red
		PrintLine "Wrong!"
		Color none
		Print "The answer is "
		Print answer
		PrintLine ""
	Stop
	
	PrintLine ""
	PrintLine "Press any key to continue..."
	Input
End

Function Menu2[op]
Begin
	Clear
	
	Color cyan
	PrintLine "Fantastic!!"
	Color none
	
	PrintLine "Press 1 to practice again."
	PrintLine "Press 2 to return to the main menu."
	PrintLine "Press 3 to exit."
	
	Var in
	Input in
	
	If in Equals 1
		Call Setup[op]
	Else in Equals 2
		Call Menu
	Else in Equals 3
		Exit
	Else
		PrintError["Unknown input!"]
	Stop
End

Function Menu
Begin
	Clear
	
	Color magenta
	PrintLine "Welcome to Math Practice!!"
	Color none
	
	PrintLine "Press 1 for addition."
	PrintLine "Press 2 for subraction."
	PrintLine "Press 3 for multiplication."
	PrintLine "Press 4 for division."
	PrintLine "Press 5 to exit."
	
	Var in
	Define in as (Call Prompt["> "])
	
	If in Equals 1
		Call Setup["+"]
	Else in Equals 2
		Call Setup["-"]
	Else in Equals 3
		Call Setup["x"]
	Else in Equals 4
		Call Setup["/"]
	Else in Equals 5
		Exit
	Else
		Call PrintError["Unknown input!"]
		Exit
	Stop
End

Call Menu

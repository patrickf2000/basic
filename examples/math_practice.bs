Comment math_practice.bs
Comment A simple math practice program
Comment At this point, we only support addition and multiplication

Function Setup
Begin
	Var op
	Set op
	
	Kill x
	Var x
	Define x as 0
	
	Clear
	Color magenta
	If op Equals "+"
		Memset "+"
		PrintLine "Addition Practice"
	Else op Equals "x"
		Memset "x"
		PrintLine "Multiplication Practice"
	Stop
	Color none
	
	Print "How many times do you wish to practice? "
	Input x

	Loop x
		Call RunPractice
	Stop
	
	Color cyan
	PrintLine "Well done!"
	Color none
	PrintLine ""
	
	Var y
	Define y as 0
	PrintLine "Press 1 to practice again."
	PrintLine "Press 2 to exit."
	Input y
	
	If y Equals 1
		Call Setup
	Else y Equals 2
		Exit
	Else
		Color red
		PrintLine "Unknown input"
		Color none
		Exit
	Stop
End

Function RunPractice
Begin
	Destroy vars
	
	Var no1
	Var no2
	Var answer
	
	Var op
	Set op
	
	Random 10
	Set no1
	
	Random 10
	Set no2
	
	If op Equals "+"
		Add no1 and no2
	Else op Equals "x"
		Mp no1 and no2
	Stop
	
	Set answer
	
	Print "Solve: "
	Print no1
	Print " "
	Print op
	Print " "
	Print no2
	Print " = "
	PrintLine ""
	
	Var in
	Define in as 0
	Input in
	
	If in Equals answer
		Color green
		PrintLine "Correct!"
		Color none
	Else
		Color red
		PrintLine "Wrong!"
		Color none
		Print "The answer is: "
		Print answer
		PrintLine ""
	Stop
	
	Memset op
	
	PrintLine "Press any key to continue..."
	Input
	Clear
End

Function Menu
Begin
	Clear
	Color cyan
	PrintLine "Welcome to Math Practice!"
	Color none
	PrintLine "Press 1 for addition practice."
	PrintLine "Press 2 for multiplication practice."
	PrintLine "Press 3 to exit."

	Var x
	Input x

	If x Equals 1
		Memset "+"
		Call Setup
	Else x Equals 2
		Memset "x"
		Call Setup
	Else x Equals 3
		Exit
	Else
		Color red
		PrintLine "Unknown input"
		Color none
		Exit
	Stop
End

Call Menu
